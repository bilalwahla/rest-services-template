buildscript {
  ext {
    springBootVersion = '1.2.7.RELEASE'
  }
  repositories {
    jcenter()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath('org.asciidoctor:asciidoctor-gradle-plugin:1.5.2')
    classpath('io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE')
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'

ext {
  snippetsDir = file('build/generated-snippets')
}

test {
  outputs.dir snippetsDir
}

asciidoctor {
  sourceDir 'src/main/asciidoc'
  attributes 'snippets': snippetsDir
  inputs.dir snippetsDir
  dependsOn test
}

jar {
  baseName = 'rest-services-template'
  version = '1.0.0-SNAPSHOT'
  dependsOn asciidoctor
  from("${asciidoctor.outputDir}/html5") {
    into 'static/docs'
  }
}

dependencies {
  compile('org.springframework.boot:spring-boot-starter-actuator')
  compile('org.springframework.boot:spring-boot-starter-data-jpa')
  compile('org.springframework.boot:spring-boot-starter-data-rest')
  compile('org.springframework.boot:spring-boot-starter-hateoas')

  compile('com.jayway.jsonpath:json-path')
  compile('org.postgresql:postgresql:9.4-1201-jdbc41')
  compile('com.h2database:h2')

  testCompile('org.springframework.boot:spring-boot-starter-test')
  testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.0.0.RELEASE')
}

jacocoTestReport {
  reports {
    xml.enabled false
    csv.enabled false
    html.destination "${buildDir}/jacocoHtml"
  }
}
check.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
  gradleVersion = '2.7'
}
